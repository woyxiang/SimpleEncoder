[ Description ("A Win32 native CheckBox") ]
[ WindowsControl ("/miscellaneous/ICONS??/CheckBox??.png") ]
[ ClassId ("33AD4EF8-6699-11CF-B70C-00AA0060D393") ]
[ InterfaceId ("7223B97B-A940-4024-8705-56552EB4299A") ]    ' FIXME implement {33AD4EF9-6699-11CF-B70C-00AA0060D393} for backcompat
[ COMCreatable (False) ]
[ EventsUseDispInterface ]
[ ComImport (True) ]
Class CheckBox
     
    #Region "INHERITANCE"
    
        [ WithDispatchForwarding ] Implements Control
        Implements WindowsControls.IWindowsControl
        Implements WinNativeFormsBase.BaseControlFocusable Via _BaseControl = New WinNativeFormsBase.BaseControlFocusable(ControlTypeConstants.vbCheckBox)
        Implements ButtonBase Via _ButtonBase = New ButtonBase
        Implements WithEvents DataFieldBinderBase Via _DataFieldBinderBase = New DataFieldBinderBase

    #End Region
        
    #Region "STATE"
        
        [ Description ("") ]
        [ Serialize (True, "Alignment") ]
            Private AlignmentINIT As VBRUN.AlignmentConstantsNoCenter = VBRUN.AlignmentConstantsNoCenter.tbLeftJustify
        [ Serialize (True, "Caption") ]
            Private Caption_INIT As String
        [ Serialize (True, "Value") ]
            Private Value_INIT As VBRUN.CheckBoxConstants = VBRUN.CheckBoxConstants.vbUnchecked

            Private WithEvents RootWindowElement As WindowsControls.WindowElement
            Private InternalValue As CheckBoxConstants = VBRUN.CheckBoxConstants.vbUnchecked        ' Only used for owner-drawn state
            Private OLEDragDropHandler As WinNativeFormsBASE.OLEDragDropHandler
            Private IsDesignMode As Boolean

        Public CausesValidation As Boolean = True
        
            Public HelpContextID As Long

        [ CustomDesigner ("designer_RestrictedOLEDropMode") ]
            Public OLEDropMode As VBRUN.OLEDropConstants
        [ Unimplemented ]
            Public RightToLeft As Boolean

            Public WhatsThisHelpID As Long
            
        Public Sub OLEDrag()
            InternalOLEDrag(Me)
        End Sub
                        
        Public Sub ShowWhatsThis()
            HelpSystem.ShowControlHelp(Me.Parent, Me, True)
        End Sub
            
    #End Region

    #Region "EVENTS"
    
        [ DefaultDesignerEvent ]
        [ Description ("") ]
            Event Click()
        [ Description ("") ]
            Event GotFocus()
        [ Description ("") ]
            Event KeyDown(KeyCode As Integer, Shift As Integer)
        [ Description ("") ]
            Event KeyPress(KeyAscii As Integer)
        [ Description ("") ]
            Event KeyUp(KeyCode As Integer, Shift As Integer)
        [ Description ("") ]
            Event LostFocus()
        [ Description ("") ]
            Event MouseDown(Button As Integer, Shift As Integer, X As Single, Y As Single)
        [ Description ("") ]
            Event MouseMove(Button As Integer, Shift As Integer, X As Single, Y As Single)
        [ Description ("") ]
            Event MouseUp(Button As Integer, Shift As Integer, X As Single, Y As Single)
        [ Description ("") ]
            Event Initialize()
        [ Description ("") ]
            Event DragDrop(Source As Control, X As Single, Y As Single)
        [ Description ("") ]
            Event DragOver(Source As Control, X As Single, Y As Single, State As Integer)
        
        Event OLECompleteDrag(Effect As Long)
        Event OLEDragDrop(Data As DataObject, Effect As Long, Button As Integer, Shift As Integer, X As Single, Y As Single)
        Event OLEDragOver(Data As DataObject, Effect As Long, Button As Integer, Shift As Integer, X As Single, Y As Single, State As Integer)
        Event OLEGiveFeedback(Effect As Long, DefaultCursors As Boolean)
        Event OLESetData(Data As DataObject, DataFormat As Integer)
        Event OLEStartDrag(Data As DataObject, AllowedEffects As Long)
        Event Validate(Cancel As Boolean)
        
    #End Region
               
    #Region "MEMBERS"

        Public Sub New()
            ' This constructor is useful for allowing control inheritance
        End Sub
        
        Private Sub InitializeDefaultValues()
        	Caption_INIT = "CheckBox"
        End Sub
    
        Public Sub New(Serializer As WindowsControls.SerializationInfo)
            If Serializer Is Nothing Then Exit Sub
            If Not Serializer.Deserialize(Me) Then
                InitializeDefaultValues
            End If
            IsDesignMode = Serializer.IsDesignMode
        End Sub
        
        Private Sub Class_Terminate()
            #If LOG_TERMINATE Then
                Debug.Print CurrentComponentName & "." & CurrentProcedureName
            #End If
        End Sub
        
        Private Sub HandleInitialize(ByVal ControlContext As WindowsControls.WindowsControlContext) _
                Implements IWindowsControl.Initialize
                
            ControlContext.SetControlArrayIndex(Me.IndexINIT)
                
            Dim styles As Long = GetStyles()
            Dim extendedStyles As Long
            extendedStyles += If(ControlContext.IsPlacedOnUserControl, 0, WinNativeFormsBASE.GeneralConsts.WindowExtendedStyles.WS_EX_NOPARENTNOTIFY)
                
            Set Me.RootWindowElement = CreateRootWindowElement(ControlContext, "ThunderCheckBox", Me.Caption_INIT, styles, extendedStyles)
        End Sub
        
        Private Function GetStyles() As Long
            Dim styles As Long
        	If Me.Style = ButtonConstants.vbButtonGraphical Then
                styles += ButtonStyles.BS_OWNERDRAW
            Else
                styles += ButtonStyles.BS_3STATE      ' FIXME VB6 uses BS_AUTO3STATE
                styles += ButtonStyles.BS_VCENTER
                styles += ButtonStyles.BS_MULTILINE
                styles += If(Me.AlignmentINIT = tbRightJustify, ButtonStyles.BS_LEFTTEXT, 0)
                styles += If(Me.Appearance = AppearanceConstants.vbAppearFlat, ButtonStyles.BS_FLAT, 0)
            End If
            Return styles
        End Function
                
        Private Sub HandleDestroy() _
                Implements IWindowsControl.Destroy
            #If LOG_TERMINATE Then
                Debug.Print CurrentComponentName & "." & CurrentProcedureName
            #End If
                
            ' disconnect anything that causes a circular reference here
            If OLEDragDropHandler IsNot Nothing Then OLEDragDropHandler.Disconnect()
            Set _ButtonBase = New ButtonBase
        End Sub
        
        Private Sub HandleCreate() _
                Handles RootWindowElement.Create
                
            Me.InitializeBASE(Me.RootWindowElement, Me.ControlContext, Me)
            
            Me.Value = Me.Value_INIT
            SyncOLEDropMode()
            
            If IsDesignMode = False Then _DataFieldBinderBase.SetupBindings()
            RaiseEvent Initialize()
        End Sub
        
        Friend Sub SyncRecreate()
            Dim Value As Long = Me.Value
            Dim Caption As String = Me.Caption
            
            RecreateWindow(GetStyles())
                
            Me.Caption = Caption
            Me.Value = Value
        End Sub

        Private Sub HandleCommand(ByVal NotificationCode As ButtonNotifications, _
                                    ByVal Identifier As Integer) _
                Handles RootWindowElement.Command
                
            Select Case NotificationCode
                Case BN_CLICKED
                    Select Case Me.Value
                        Case CheckBoxConstants.vbUnchecked
                            Me.Value = CheckBoxConstants.vbChecked
                        Case CheckBoxConstants.vbGrayed, CheckBoxConstants.vbChecked
                            Me.Value = CheckBoxConstants.vbUnchecked
                    End Select
                    RaiseEvent Click()
            End Select
        End Sub
        
        Private Sub HandleGetColors(ByVal hdc As LongPtr, ByRef BackBrushOut As LongPtr, ByVal ControlType As ControlTypeConstants) _
                Handles RootWindowElement.GetColors
                
            WinNativeFormsBASE.WindowsAPI.GDI32_SetTextForeColor(hdc, TranslateColor(Me.ForeColor))
            WinNativeFormsBASE.WindowsAPI.GDI32_SetBackMode(hdc, WinNativeFormsBASE.BackgroundModes.TRANSPARENT)
            BackBrushOut = Me.CreateBackBrush(Me.BackColor)
        End Sub

        ' FIXME consider a syntax for event forwarding...
        ' e.g. ForwardEvent RootWindowElement.GotFocus To GotFocus
        
        Private Sub HandleGotFocus() _
                Handles RootWindowElement.GotFocus
                
            RaiseEvent GotFocus()
        End Sub

        Private Sub HandleLostFocus() _
                Handles RootWindowElement.LostFocus
                
            RaiseEvent LostFocus()
        End Sub
        
        Private Sub HandleKeyDown(ByRef KeyCode As VBRUN.KeyCodeConstants, _
                                    ByVal ShiftState As VBRUN.ShiftConstants) _
                Handles RootWindowElement.KeyDown
                
            Dim KeyCodeInt As Integer = KeyCode
            RaiseEvent KeyDown(KeyCodeInt, CInt(ShiftState))
            KeyCode = KeyCodeInt
        End Sub
        
        Private Sub HandleKeyUp(ByRef KeyCode As VBRUN.KeyCodeConstants, _
                                    ByVal ShiftState As VBRUN.ShiftConstants) _
                Handles RootWindowElement.KeyUp
                
            Dim KeyCodeInt As Integer = KeyCode
            RaiseEvent KeyUp(KeyCodeInt, CInt(ShiftState))
            KeyCode = KeyCodeInt
        End Sub
        
        Private Sub HandleKeyPress(ByRef Key As Integer) _
                Handles RootWindowElement.KeyPress
                
            RaiseEvent KeyPress(Key)
        End Sub
        
        Private Sub HandleMouseDown(ByVal Button As VBRUN.MouseButtonConstants, _
                                        ByVal ShiftState As VBRUN.ShiftConstants, _
                                        ByVal X As Single, ByVal Y As Single) _
                Handles RootWindowElement.MouseDown
                
            RaiseEvent MouseDown(CInt(Button), CInt(ShiftState), X, Y)
        End Sub
        
        Private Sub HandleMouseMove(ByVal Button As VBRUN.MouseButtonConstants, _
                                        ByVal ShiftState As VBRUN.ShiftConstants, _
                                        ByVal X As Single, ByVal Y As Single) _
                Handles RootWindowElement.MouseMove
                
            RaiseEvent MouseMove(CInt(Button), CInt(ShiftState), X, Y)
        End Sub
        
        Private Sub HandleMouseUp(ByVal Button As VBRUN.MouseButtonConstants, _
                                        ByVal ShiftState As VBRUN.ShiftConstants, _
                                        ByVal X As Single, ByVal Y As Single) _
                Handles RootWindowElement.MouseUp
                
            RaiseEvent MouseUp(CInt(Button), CInt(ShiftState), X, Y)
        End Sub
        
        [ Serialize (False) ]
        Public Property Get hWnd() As LongPtr
            If Me.RootWindowElement IsNot Nothing Then
                Me.ControlContext.EnsureFormIsLoaded()
                Return Me.RootWindowElement.Handle
            End If
        End Property
            
        [ Serialize (False) ]
        Public Property Get Caption() As String
            Me.ControlContext.EnsureFormIsLoaded()
            Return WinNativeFormsBASE.WindowsAPI.USER32_GetWindowText(Me.RootWindowElement)
        End Property
        
        [ Serialize (False) ]
        Public Property Let Caption(ByVal Value As String)
            Me.ControlContext.EnsureFormIsLoaded()
            
            WinNativeFormsBASE.WindowsAPI.USER32_SendMessage(Me.RootWindowElement, WinNativeFormsBASE.WM_SETTEXT, 0, StrPtrSafe(Value))
        End Property

        [ Serialize (False) ]
        Public Property Get Value() As VBRUN.CheckBoxConstants
            Me.ControlContext.EnsureFormIsLoaded()
            If Me.Style <> ButtonConstants.vbButtonGraphical Then
                Return CLng(WinNativeFormsBASE.WindowsAPI.USER32_SendMessage(Me.RootWindowElement, ButtonMessages.BM_GETCHECK, 0, 0))
            Else
            	Return Me.InternalValue
            End If
        End Property
        
        [ Serialize (False) ]
        Public Property Let Value(ByVal Value As VBRUN.CheckBoxConstants)
            Me.ControlContext.EnsureFormIsLoaded()
            If Me.Style <> ButtonConstants.vbButtonGraphical Then
                WinNativeFormsBASE.WindowsAPI.USER32_SendMessage(Me.RootWindowElement, ButtonMessages.BM_SETCHECK, Value, 0)
            Else
            	Me.InternalValue = Value
                Me.Refresh
            End If
            _DataFieldBinderBase.OnDataChanged()
        End Property
        
        [ Serialize (False) ]
        [ DefaultMember ]
        Public Property Get _Default() As VBRUN.CheckBoxConstants
            Return Me.Value
        End Property
        
        [ Serialize (False) ]
        [ DefaultMember ]
        Public Property Let _Default(ByVal Value As VBRUN.CheckBoxConstants)
            Me.Value = Value
        End Property
        
        [ Serialize (False) ]
        [ Description ("") ]
        Property Get Alignment() As VBRUN.AlignmentConstantsNoCenter
            Me.ControlContext.EnsureFormIsLoaded()
            Dim flag As Boolean = WinNativeFormsBASE.WindowsAPI.USER32_GetStyleFlag(RootWindowElement, ButtonStyles.BS_LEFTTEXT)
            Return If(flag, VBRUN.AlignmentConstantsNoCenter.tbRightJustify, VBRUN.AlignmentConstantsNoCenter.tbLeftJustify)
        End Property
        
        [ Serialize (False) ]
        [ Description ("") ]
        Property Let Alignment(Value As VBRUN.AlignmentConstantsNoCenter)
            Me.ControlContext.EnsureFormIsLoaded()
            WinNativeFormsBASE.WindowsAPI.USER32_SetStyleFlag(RootWindowElement, ButtonStyles.BS_LEFTTEXT, Value = VBRUN.AlignmentConstantsNoCenter.tbRightJustify)
            Me.Refresh
        End Property
        
        [ Serialize (False) ]
        Public Property Get Parent() As Object ' As Form  FIXME, needs to work also for UCs
            Return ControlContext.GetForm
        End Property
        
        Private Sub HandleDragOver(ByVal Source As Object, ByVal X As Double, ByVal Y As Double, ByVal State As Long) _
                Handles RootWindowElement.DragOver
            
            If State = 3 Then
                RaiseEvent DragDrop(Source, CSng(X), CSng(Y))
            Else
                RaiseEvent DragOver(Source, CSng(X), CSng(Y), CInt(State))
            End If
        End Sub
        
        Private Sub HandleDrawItem(ByRef Info As WinNativeFormsBASE.DRAWITEMSTRUCT, Handled As Boolean) _
                Handles RootWindowElement.DrawItem
            
            HandleDrawItem_BASE(Info, Handled, Me.Value = CheckBoxConstants.vbChecked, False)
        End Sub

        Private Sub SyncOLEDropMode() _
                Handles OLEDropMode.OnPropertyLet
                
            BaseSyncOLEDropMode(Me, Me.OLEDropMode, Me.OLEDragDropHandler, False, False)
        End Sub
        
        Private Sub HandleValidate(Cancel As Boolean) _
                Handles RootWindowElement.Validate

            RaiseEvent Validate(Cancel)
        End Sub
        
        Private Sub _DataFieldBinderBase_SetLiveValue(fieldValue As Variant) Handles [_DataFieldBinderBase].SetLiveValue
            If VarType(fieldValue) <> vbBoolean Then
                Err.Raise 13     ' type mismatch
            End If
            Me.Value = If(fieldValue <> 0, vbChecked, vbUnchecked)
        End Sub
        
        Private Sub _DataFieldBinderBase_GetLiveValue(fieldValue As Variant) Handles [_DataFieldBinderBase].GetLiveValue
            fieldValue = If(Me.Value <> vbUnchecked, True, False)
        End Sub
        
        Private Sub _DataFieldBinderBase_GetParent(out As Control) Handles [_DataFieldBinderBase].GetParent
            Set out = Me.Parent
        End Sub
        
    #End Region
    
End Class